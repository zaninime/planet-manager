apply plugin: 'com.android.application'

import groovy.json.JsonSlurper

def version() {
    def jsonSlurper = new JsonSlurper()
    return jsonSlurper.parse(new File('../package.json'))
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"

    defaultConfig {
        def v = version()
        applicationId "eu.elos.planetmgr.app"
        minSdkVersion 19
        targetSdkVersion 24
        if (System.getenv("BUILD_NUMBER") != null) {
            versionCode 400000 + Integer.parseInt(System.getenv("BUILD_NUMBER"))
        } else {
            versionCode 400000
        }
        versionName v.version
    }
    signingConfigs {
        if (System.getenv("KEYSTORE") != null) {
            release {
                storeFile file(System.getenv("KEYSTORE"))
                storePassword System.getenv("KEYSTORE_PASSWORD")
                keyAlias System.getenv("KEY_ALIAS")
                keyPassword System.getenv("KEY_PASSWORD")
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (System.getenv("KEYSTORE") != null) {
                signingConfig signingConfigs.release
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'

    compile 'com.android.support:appcompat-v7:24.2.0'
    compile 'com.google.code.gson:gson:2.7'
}
